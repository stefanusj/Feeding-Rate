
FUNCTION_BLOCK tipper

// Deklarasi variable input
VAR_INPUT
	temperature : REAL;
	oxygen : REAL;
END_VAR

// Deklarasi variable output
VAR_OUTPUT
	feeding : REAL;
END_VAR

// Variable input untuk 'temperature': {'very low', 'low' , 'medium', 'high', 'very high'}
FUZZIFY temperature
	TERM very_low := (0, 1) (20, 1) (24, 0);
	TERM low := (20, 0) (24, 1) (26, 0);
	TERM medium := (24, 0) (26, 1) (28, 0);
	TERM high := (26, 0) (28, 1) (30, 0);
	TERM very_high := (28, 0) (30, 1) (100, 1);
END_FUZZIFY

// Variabel input untuk 'oxygen': {'very low', 'low' , 'medium', 'high', 'very high'}
FUZZIFY oxygen
	TERM very_low := (0, 1) (20, 1) (40, 0);
    TERM low := (20, 0) (40, 1) (60, 0);
    TERM medium := (40, 0) (60, 1) (80, 0);
    TERM high := (60, 0) (80, 1) (100, 0);
    TERM very_high := (80, 0) (100, 1) (100, 1);
END_FUZZIFY


// Variabel output untuk 'feeding' : {'very low', 'low' , 'medium', 'high', 'very high'}
DEFUZZIFY feeding
	TERM very_low := (2, 0) (2.5, 1) (3, 0);
    TERM low := (2.5, 0) (3, 1) (3.5, 0);
    TERM medium := (3, 0) (3.5, 1) (4, 0);
    TERM high := (3.5, 0) (4, 1) (4.5, 0);
    TERM very_high := (4, 0) (4.5, 1) (5, 0);

	METHOD : COG;		// Menggunakan metode defuzzyfikasi 'Center Of Gravity'
	DEFAULT := 0;		// Nilai default 0 (Jika tidak ada rule yang mengaktivasi defuzzyfikasi)
END_DEFUZZIFY

// Inference rules
RULEBLOCK No1
	AND : MIN;	// Use 'min' for 'and'
	ACT : MIN;	// Use 'min' activation method
	ACCU : MAX;	// Use 'max' accumulation method

	RULE 1 : IF (temperature IS very_low) AND (oxygen IS very_low) THEN feeding IS very_low;
	RULE 2 : IF (temperature IS very_low) AND (oxygen IS low) THEN feeding IS very_low;
	RULE 3 : IF (temperature IS very_low) AND (oxygen IS medium) THEN feeding IS very_low;
	RULE 4 : IF (temperature IS very_low) AND (oxygen IS high) THEN feeding IS very_low;
	RULE 5 : IF (temperature IS very_low) AND (oxygen IS very_high) THEN feeding IS very_low;
	RULE 6 : IF (temperature IS low) AND (oxygen IS very_low) THEN feeding IS very_low;
	RULE 7 : IF (temperature IS low) AND (oxygen IS low) THEN feeding IS low;
	RULE 8 : IF (temperature IS low) AND (oxygen IS medium) THEN feeding IS low;
	RULE 9 : IF (temperature IS low) AND (oxygen IS high) THEN feeding IS low;
	RULE 10 : IF (temperature IS low) AND (oxygen IS very_high) THEN feeding IS low;
	RULE 11 : IF (temperature IS medium) AND (oxygen IS very_low) THEN feeding IS very_low;
	RULE 12 : IF (temperature IS medium) AND (oxygen IS low) THEN feeding IS low;
	RULE 13 : IF (temperature IS medium) AND (oxygen IS medium) THEN feeding IS medium;
	RULE 14 : IF (temperature IS medium) AND (oxygen IS high) THEN feeding IS medium;
	RULE 15 : IF (temperature IS medium) AND (oxygen IS very_high) THEN feeding IS medium;
	RULE 16 : IF (temperature IS high) AND (oxygen IS very_low) THEN feeding IS very_low;
	RULE 17 : IF (temperature IS high) AND (oxygen IS low) THEN feeding IS low;
	RULE 18 : IF (temperature IS high) AND (oxygen IS medium) THEN feeding IS medium;
	RULE 19 : IF (temperature IS high) AND (oxygen IS high) THEN feeding IS high;
	RULE 20 : IF (temperature IS high) AND (oxygen IS very_high) THEN feeding IS high;
	RULE 21 : IF (temperature IS very_high) AND (oxygen IS very_low) THEN feeding IS very_low;
	RULE 22 : IF (temperature IS very_high) AND (oxygen IS low) THEN feeding IS low;
	RULE 23 : IF (temperature IS very_high) AND (oxygen IS medium) THEN feeding IS medium;
	RULE 24 : IF (temperature IS very_high) AND (oxygen IS high) THEN feeding IS high;
	RULE 25 : IF (temperature IS very_high) AND (oxygen IS very_high) THEN feeding IS very_high;
END_RULEBLOCK

END_FUNCTION_BLOCK